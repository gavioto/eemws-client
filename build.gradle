plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
    id 'org.sonarqube' version '2.0.1'
    id 'com.bmuschko.nexus' version '2.3.1'

}

description = 'eemws-client-parent'

scmVersion {
    tag {
        prefix = ''
    }

    // Incrementing policy
    versionIncrementer 'incrementMinor'  // default
    branchVersionIncrementer = [
       'master': 'incrementPrerelease',
       'release/.*': 'incrementPrerelease',
       'hotfix/.*': 'incrementPrerelease'
    ]

    // Decorators
    versionCreator 'simple'
    //branchVersionCreator = [
    //   'feature/.*': 'versionWithBranch'
    //]

    localOnly = true  // no pushing to remote

    ignoreUncommittedChanges = false  // add -SNAPSHOT if uncommited changes
}

ext {
  eemws_core_version = '1.1.2'  // used in subprojects dependencies
}

allprojects  {
  version = scmVersion.version
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  //apply plugin: 'maven'

  apply plugin: 'com.bmuschko.nexus'

  group = 'es.ree.eemws'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7


  repositories {
    mavenLocal()

    maven { url "http://repo.maven.apache.org/maven2" }
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  modifyPom {
    project {
         //name 'eemws-client-parent'
         //description 'eemws-client libraries'
         url 'https://bitbucket.org/smree/eemws-client'
         inceptionYear '2014'

         scm {
             url 'https://bitbucket.org/smree/eemws-client'
             connection 'scm:https://bitbucket.org/smree/eemws-client.git'
             developerConnection 'scm:git://bitbucket.org:smree/eemws-client.git'
         }

         licenses {
             license {
                 name 'GNU General Lesser Public License (LGPL) version 3.0'
                 url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                 distribution 'repo'
             }
         }

         developers {
             developer {
                 id 'e-jimenez'
                 name 'Enrique Jiménez Raya'
                 email 'ejimenez@ree.es'
             }
             developer {
                 id 'agusmba'
                 name 'Agustín Martín Barbero'
                 email 'ambarbero@ree.es'
             }
         }
    }
  }
}

logger.lifecycle "-------------------------------------------------" + '-' * version.toString().length()
logger.lifecycle " IEC62325-504 client libraries + kit - version: $version "
logger.lifecycle "-------------------------------------------------" + '-' * version.toString().length()
