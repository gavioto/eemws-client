plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.3.4'
    id 'org.sonarqube' version '2.0.1'
}

description = 'eemws-client-parent'

scmVersion {
    tag {
        prefix = ''
    }

    // Incrementing policy
    versionIncrementer 'incrementMinor'  // default
    branchVersionIncrementer = [
       'master': 'incrementPrerelease',
       'release/.*': 'incrementPrerelease',
       'hotfix/.*': 'incrementPrerelease'
    ]

    // Decorators
    versionCreator 'simple'
    //branchVersionCreator = [
    //   'feature/.*': 'versionWithBranch'
    //]

    localOnly = true  // no pushing to remote

    ignoreUncommittedChanges = false  // add -SNAPSHOT if uncommited changes
}

ext {
  eemws_core_version = '1.1.0-rc1'  // used in subprojects dependencies
}

allprojects  {
  apply plugin: 'maven'

  group = 'eemws'
  version = scmVersion.version
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'
  
  sourceCompatibility = 1.7
  targetCompatibility = 1.7


  repositories {
    mavenLocal()

    maven { url "http://repo.maven.apache.org/maven2" }
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

}

println "-------------------------------------------------" + '-' * version.toString().length()
println " IEC62325-504 client libraries + kit - version: $version "
println "-------------------------------------------------" + '-' * version.toString().length()
